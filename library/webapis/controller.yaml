apiVersion: ctrl.declare.dev/v1
kind: Controller
metadata:
  name: webapis
spec:
  crdName: webapis.apps.example.com
  children:
  - apiVersion: apps/v1
    kind: Deployment
  - apiVersion: autoscaling/v1
    kind: HorizontalPodAutoscaler
  - apiVersion: v1
    kind: Service
  - apiVersion: networking.k8s.io/v1
    kind: NetworkPolicy
  - apiVersion: networking.k8s.io/v1beta1
    kind: Ingress
  config:
  - configMap: webapis
  source:
    controller.jsonnet: |
      function(request) {
        local obj = request.object,
        local cfg = request.config,

        local containerPort = if std.objectHas(obj.spec, 'port') && obj.spec.port != 0 then obj.spec.port else 8080,

        local minCPU = if std.objectHas(obj.spec, 'resources') && std.objectHas(obj.spec.resources, 'min') && std.objectHas(obj.spec.resources.min, 'cpu') then obj.spec.resources.min.cpu else "200m",
        local maxCPU = if std.objectHas(obj.spec, 'resources') && std.objectHas(obj.spec.resources, 'max') && std.objectHas(obj.spec.resources.max, 'cpu') then obj.spec.resources.max.cpu else "500m",

        local readinessProbe = if std.objectHas(obj.spec, 'healthcheck') && std.objectHas(obj.spec.healthcheck, 'path') then {httpGet: { path: obj.spec.healthcheck.path, port: containerPort }} else {tcpSocket: { port: containerPort }},

        local deployment = {
          apiVersion: 'apps/v1',
          kind: 'Deployment',
          metadata: {
            name: obj.metadata.name,
            labels: {
              app: obj.metadata.name,
            }
          },
          spec: {
            selector: {
              matchLabels: {
                app: obj.metadata.name,
              },
            },
            template: {
              metadata: {
                labels: {
                  app: obj.metadata.name,
                },
              },
              spec: {
                containers: [
                  {
                    name: 'app',
                    image: obj.spec.image,
                    ports: [
                      {
                        name: 'app',
                        containerPort: containerPort,
                        protocol: 'TCP',
                      },
                    ],
                    readinessProbe: readinessProbe,
                    resources: {
                      requests: {
                        cpu: minCPU,
                      },
                      limits: {
                        cpu: maxCPU,
                      },
                    },
                  },
                ],
              },
            },
          },
        },
        local currentDeployment = std.native("getObject")(deployment),
        local currentReadyReplicas = if std.objectHas(currentDeployment, 'status') && std.objectHas(currentDeployment.status, 'readyReplicas') then currentDeployment.status.readyReplicas else 0,

        local service = {
          apiVersion: 'v1',
          kind: 'Service',
          metadata: {
            name: obj.metadata.name,
          },
          spec: {
            selector: {
              app: obj.metadata.name,
            },
            ports: [
              {
                targetPort: 'app',
                # Clients should use standardized port.
                port: 80,
                protocol: 'TCP',
              },
            ],
          },
        },

        local isExposed = std.objectHas(obj.spec, 'public') && obj.spec.public == true,

        local npPorts = [
           {
              protocol: 'TCP',
              port: obj.spec.port
           }
        ],

        local netpol = {
           apiVersion: 'networking.k8s.io/v1',
           kind: 'NetworkPolicy',
           metadata: {
              name: obj.metadata.name,
           },
           spec: {
              podSelector: {
                 matchLabels: {
                    app: obj.metadata.name,
                 }
              },
              policyTypes: [
                 'Ingress',
              ],
              ingress: [
                 {
                    from: [
                       {
                          podSelector: {
                             matchLabels: {
                                app: c.app,
                             }
                          },
                          namespaceSelector: {},
                       }
                    ],
                    ports: npPorts,
                 }
                 for c in obj.spec.allowedClients
              ] + (if isExposed then [{
                from: [
                  {
                    # Allow ingress from ingress controller.
                    # TODO: Account for more ingress controllers.
                    namespaceSelector: {
                      matchLabels: {
                        "app.kubernetes.io/name": "ingress-nginx",
                      }
                    }
                  }
                ],
                ports: npPorts,
              }] else []),
           },
        },

        local ingress = {
           apiVersion: "networking.k8s.io/v1beta1",
           kind: "Ingress",
           metadata: {
              name: obj.metadata.name,
              annotations: {
                 "nginx.ingress.kubernetes.io/rewrite-target": "/"
              }
           },
           spec: {
              rules: [
                 {
                    http: {
                       paths: [
                          {
                             path: "/testpath",
                             pathType: "Prefix",
                             backend: {
                                serviceName: obj.metadata.name,
                                servicePort: "app",
                             },
                          },
                       ]
                    },
                 },
              ],
           },
        },

        local minReplicas = if std.objectHas(cfg, 'minReplicas') then std.parseInt(cfg.minReplicas) else 1,
        local maxReplicas = if std.objectHas(cfg, 'maxReplicas') then std.parseInt(cfg.maxReplicas) else 10,

        local hpa = {
          apiVersion: "autoscaling/v1",
          kind: "HorizontalPodAutoscaler",
          metadata: {
            name: obj.metadata.name,
          },
          spec: {
            minReplicas: minReplicas,
            maxReplicas: maxReplicas,
            scaleTargetRef: {
              apiVersion: "apps/v1",
              kind: "Deployment",
              name: obj.metadata.name,
            },
            targetCPUUtilizationPercentage: 50,
          },
        },
        local currentHPA = std.native("getObject")(hpa),
        local currentReplicas = if std.objectHas(currentHPA, 'status') && std.objectHas(currentHPA.status, 'currentReplicas') then currentHPA.status.currentReplicas else 0,
        local currentCPUUtilizationPercentage = if std.objectHas(currentHPA, 'status') && std.objectHas(currentHPA.status, 'currentCPUUtilizationPercentage') then currentHPA.status.currentCPUUtilizationPercentage else 0,

        children: [
          deployment,
          hpa,
          service,
          netpol,
        ] + (if isExposed then [ingress] else []),

        status: { replicas: currentReplicas, healthy: (currentReadyReplicas > 0), cpuUtilizationPercentage: currentCPUUtilizationPercentage },
      }
  
  
